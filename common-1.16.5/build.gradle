buildscript {
	repositories {
		maven {
			url = "https://repo.spongepowered.org/repository/maven-public/"
			content {
				includeGroup "org.spongepowered"
				includeGroup "net.minecraftforge" //vanillagradle dep
			}
		}
		gradlePluginPortal()
	}
	dependencies {
		classpath "org.spongepowered:vanillagradle:0.2.1-SNAPSHOT"
	}
}

evaluationDependsOn(":core") // fuck gradle episode 151905105

apply plugin: "java"
apply plugin: "org.spongepowered.gradle.vanilla"

archivesBaseName = "${project.modId}-common-1.16.5"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	options.release = 8
}
java.withSourcesJar()

minecraft {
	version("1.16.5")
}

repositories {
	maven {
		url = "https://repo.spongepowered.org/repository/maven-public/"
		content { includeGroup "org.spongepowered" }
	}
}

dependencies {
	compileOnly "org.spongepowered:mixin:0.8.5"
	compileOnly "org.jetbrains:annotations:24.0.1"
	
	implementation project(":core")
}

processResources {
	from project(":core").sourceSets.main.resources
}

tasks.withType(GenerateModuleMetadata) {
	enabled = false
}