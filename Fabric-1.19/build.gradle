plugins {
	id "fabric-loom" version "1.0-SNAPSHOT"
}

archivesBaseName = "${project.modId}-fabric-${project.minecraftVersion}"

/// deps ///

dependencies {
	//basics
	minecraft "com.mojang:minecraft:${project.minecraftVersion}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.fabricLoaderVersion}"
	
	//fabric api, TODO trim down to required modules
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabricApiVersion}"
	
	//xplat
	implementation project(":Xplat-Xver")
	implementation project(":Xplat-1.19")
}

//fold in xplat resources and sources

processResources {
	from project(":Xplat-Xver").sourceSets.main.resources
	from project(":Xplat-1.19").sourceSets.main.resources
}

tasks.withType(JavaCompile) {
	source(project(":Xplat-Xver").sourceSets.main.allSource)
	source(project(":Xplat-1.19").sourceSets.main.allSource)
}

//loom doesn't generate run configs by default in subprojects
loom {
	runs {
		"client 1.19" {
			client()
			setConfigName("Fabric Client 1.19")
			ideConfigGenerated(true)
			runDir("run")
		}
		"server 1.19" {
			server()
			setConfigName("Fabric Server 1.19")
			ideConfigGenerated(true)
			runDir("run")
		}
	}
	
	//nor can it figure out the refmap name in this situation
	mixin {
		defaultRefmapName = "${project.modId}.refmap.json"
	}
}