buildscript {
	repositories {
		maven { url = "https://maven.fabricmc.net/"}
		maven { url = "https://repo.sleeping.town/" }
		gradlePluginPortal()
	}
	dependencies {
		classpath "agency.highlysuspect:minivan:0.2"
		classpath "agency.highlysuspect:crossroad:0.3"
	}
}

evaluationDependsOn(":core") // i love gradle :)

apply plugin: "java"
apply plugin: "agency.highlysuspect.minivan"
apply plugin: "agency.highlysuspect.crossroad"

archivesBaseName = "${project.modId}-core-plus-minecraft"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	sourceCompatibility = "8"
	targetCompatibility = "8"
}
java.withSourcesJar()

repositories {
	maven {
		url = "https://repo.spongepowered.org/repository/maven-public/"
		content { includeGroup "org.spongepowered" }
	}
}

dependencies {
	compileOnly "org.spongepowered:mixin:0.8.5"
	compileOnly "org.jetbrains:annotations:24.0.1"
	
	def a = minivan.getMinecraft("1.16.5")
	def b = minivan.getMinecraft("1.18.2")
	def c = minivan.getMinecraft("1.19.2")
	def d = minivan.getMinecraft("1.19.4")
	def merged = crossroad.merge(a.minecraft, b.minecraft, c.minecraft, d.minecraft)
	
	compileOnly project.files(merged)
	a.dependencies.each { compileOnly it }
	
	implementation project(":core")
}

processResources {
	from project(":core").sourceSets.main.resources
}

tasks.withType(GenerateModuleMetadata) {
	enabled = false
}