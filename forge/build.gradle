buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net' }
		maven {
			url = 'https://repo.spongepowered.org/repository/maven-public/'
			content { includeGroup "org.spongepowered" }
		}
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}
apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'org.spongepowered.mixin'

archivesBaseName = "${project.mod_name}-${project.minecraft_version}-${project.version}-forge"

mixin {
	add sourceSets.main, "apathy.refmap.json"
	
	config "apathy.mixins-common.json"
	config "apathy.mixins-forge.json"
}

minecraft {
	mappings channel: 'official', version: minecraft_version
	
//	if (project.hasProperty('forge_ats_enabled') && project.findProperty('forge_ats_enabled').toBoolean()) {
//		// This location is hardcoded in Forge and can not be changed.
//		// https://github.com/MinecraftForge/MinecraftForge/blob/be1698bb1554f9c8fa2f58e32b9ab70bc4385e60/fmlloader/src/main/java/net/minecraftforge/fml/loading/moddiscovery/ModFile.java#L123
//		accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
//		project.logger.debug('Forge Access Transformers are enabled for this project.')
//	}
	
	runs {
		client {
			workingDirectory project.file('run')
			ideaModule "${rootProject.name}.${project.name}.main"
			taskName 'Client'
			args "-mixin.config=apathy.mixins-common.json", "-mixin.config=apathy.mixins-forge.json"
			mods {
				modClientRun {
					source sourceSets.main
					source project(":common").sourceSets.main
				}
			}
		}
		
		server {
			workingDirectory project.file('run')
			ideaModule "${rootProject.name}.${project.name}.main"
			taskName 'Server'
			args "-mixin.config=apathy.mixins-common.json", "-mixin.config=apathy.mixins-forge.json"
			mods {
				modServerRun {
					source sourceSets.main
					source project(":common").sourceSets.main
				}
			}
		}
	}
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
	minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
	compileOnly project(":common")
	
	//Oops all refmaps
	annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

tasks.withType(JavaCompile) {
	source(project(":common").sourceSets.main.allSource)
}

processResources {
	from project(":common").sourceSets.main.resources
	inputs.property "version", project.version
	
	filesMatching(["mods.toml", ".*mods.toml", "**/mods.toml"]) {//Fucing work please!!!!!!!!!!
		expand "version": project.version
	}
	
	//Crusty refmap things
	filesMatching(["apathy.mixins-common.json", "apathy.mixins-forge.json"]) {
		expand "refmap_target": "apathy.refmap.json" //set in the annotation processor above
	}
}

jar {
	manifest {
		attributes([
			"Specification-Title": "Apathy",
			"Specification-Vendor": "quaternary",
			"Specification-Version": "1",
			"Implementation-Title": "Apathy",
			"Implementation-Version": "${archiveVersion.get()}",
			"Implementation-Vendor": "quaternary"
		])
	}
	finalizedBy 'reobfJar'
}