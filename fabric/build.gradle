plugins {
	id "fabric-loom" version "0.10-SNAPSHOT"
	id "maven-publish"
	id "idea"
}

archivesBaseName = "${project.modId}-${project.minecraftVersion}-fabric"

/// deps ///

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraftVersion}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.fabricLoaderVersion}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabricApiVersion}"
	
	implementation project(":common")
}

tasks.withType(JavaCompile) {
	source(project(":common").sourceSets.main.allSource)
}

/// resources ///

processResources {
	//include common resources
	from project(":common").sourceSets.main.resources
	
	//substutute in version
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

loom {
	/// mixin ///
	
	mixin {
		defaultRefmapName = "${project.modId}.refmap.json"
	}
	
	/// runs ///
	runs {
		client {
			client()
			setConfigName("Fabric Client")
			ideConfigGenerated(true)
			runDir("run")
		}
		server {
			server()
			setConfigName("Fabric Server")
			ideConfigGenerated(true)
			runDir("run")
		}
	}
}