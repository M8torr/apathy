buildscript {
	repositories {
		maven {
			url = "https://maven.fabricmc.net/"
			content {
				includeGroup "net.fabricmc"
				includeGroup "fabric-loom"
			}
		}
		mavenCentral()
	}
	dependencies {
		classpath "fabric-loom:fabric-loom.gradle.plugin:1.1.10"
	}
}

apply plugin: "java"
apply plugin: "fabric-loom"

archivesBaseName = "${project.modId}-fabric-1.16.5"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	sourceCompatibility = "8"
	targetCompatibility = "8"
}
java.withSourcesJar()

//TODO (amalgamation): Remove this version-specific override
version = "1.1.2"

/// deps ///

dependencies {
	//basics
	minecraft "com.mojang:minecraft:1.16.5"
	mappings "net.fabricmc:yarn:1.16.5+build.9:v2" //TODO (amalgamation) fuck Yarn
	modImplementation "net.fabricmc:fabric-loader:0.11.3"
	compileOnly "org.jetbrains:annotations:24.0.1"
	
	//fabric api, TODO trim down to required modules
	modImplementation "net.fabricmc.fabric-api:fabric-api:0.34.2+1.16"
	
	//xplat
	//implementation project(":common-1.16.5")
}

//fold in xplat resources and sources
processResources {
	//from project(":common-1.16.5").sourceSets.main.resources //TODO (amalgamation)
	
	inputs.property "version", project.version
	filesMatching("**/fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile) {
	//source(project(":common-1.16.5").sourceSets.main.allSource) //TODO (amalgamation)
}

//loom doesn't generate run configs by default in subprojects
loom {
	runs {
		client {
			client()
			setConfigName("Fabric 1.16.5 Client")
			ideConfigGenerated(true)
			runDir("run")
		}
		server {
			server()
			setConfigName("Fabric 1.16.5 Server")
			ideConfigGenerated(true)
			runDir("run")
		}
	}
	
	//nor can it figure out the refmap name in this situation
	mixin {
		defaultRefmapName = "${project.modId}.refmap.json"
		
		//see https://github.com/FabricMC/fabric-loom/issues/456
		//todo, maybe also fixable by updating floader?
		useLegacyMixinAp = true
	}
}

tasks.withType(GenerateModuleMetadata) {
	enabled = false
}